[COVARIATE]
input = wt

EQUATION:
lw70 = log(wt/70)

[INDIVIDUAL]
input = {Tlag_pop, omega_Tlag, ka_pop, omega_ka, V_pop, beta_V_lw70, lw70, omega_V, Cl_pop, beta_Cl_lw70, omega_Cl, ke0_pop, omega_ke0, alpha_pop, omega_alpha, beta_pop, gamma_pop}

DEFINITION:
Tlag = {distribution=lognormal, typical=Tlag_pop, sd=omega_Tlag}
ka = {distribution=lognormal, typical=ka_pop, sd=omega_ka}
V = {distribution=lognormal, typical=V_pop, covariate=lw70, coefficient=beta_V_lw70, sd=omega_V}
Cl = {distribution=lognormal, typical=Cl_pop, covariate=lw70, coefficient=beta_Cl_lw70, sd=omega_Cl}
ke0 = {distribution=lognormal, typical=ke0_pop, sd=omega_ke0}
alpha = {distribution=normal, typical=alpha_pop, sd=omega_alpha}
beta = {distribution=normal, typical=beta_pop, no-variability}
gamma = {distribution=lognormal, typical=gamma_pop, no-variability}

[LONGITUDINAL]
input = {a, b}

input = {Tlag, ka, V, Cl, ke0, alpha, beta, gamma}

EQUATION:
{Cc,Ce}= pkmodel(Tlag,ka,V,Cl,ke0)
lp1 = alpha + beta*Ce
lp2 = lp1+ gamma         ; gamma >= 0

DEFINITION:
Level = {
	type=categorical
	categories={1,2,3}
	logit(P(Level<=1)) = lp1
	logit(P(Level<=2)) = lp2
}

DEFINITION:
Concentration = {distribution=normal, prediction=Cc, errorModel=combined1(a, b)}

